class Octabrain : DukeMonster
{
	override void Tick()
	{
		// don't allow random traveling, only A_Chase
		if (health > 0)
			vel /= 2;
		
		Super.Tick();
	}

	Default
	{
		Monster;
		Radius 24;
		Height 64;
		Health 175;
		Mass 200;
		PainChance 64;
		Speed 4;
		
		DamageFactor "OctaAt", 0.25;
		
		+NOGRAVITY;
		+FLOAT;
		
		Scale 0.7;
		
		Obituary "%o got %p brain fried by an Octabrain.";
		HitObituary "%o was eaten by an Octabrain.";
		
		SeeSound "octa/see";
		PainSound "octa/pain";
		DeathSound "octa/death";
		ActiveSound "octa/roam";
	}

	States
	{
	Spawn:
		OCTA A 8 A_Look;
		loop;
		
	See:
		OCTA AAAAAAAABBBBBBBB 1
		{
			if (CheckShrunk())
				return A_Chase(null, null);
			else return A_Chase("Melee", "Missile");
		}
		loop;
		
	Melee:
		OCTA ABC 4 A_FaceTarget;
		OCTA D 8 A_CustomMeleeAttack(random(11, 22), "octa/melee");
		OCTA BA 2;
		goto See;
		
	Missile:
		TNT1 A 0 A_PlaySound("octa/missile", CHAN_AUTO);
		OCTA ABCD 4 A_FaceTarget;
		OCTA E 8 BRIGHT A_FaceTarget;
		OCTA E 4 BRIGHT { A_FaceTarget(); A_SpawnProjectile("OctabrainBall"); }
		OCTA DCBA 2;
		goto See;
	
	Pain:
		OCTA F 8 A_Pain;
		goto See;
		
	Death:
		TNT1 A 0 A_Scream;
		OCTA FGHI 8;
		OCTA J 8 A_NoBlocking;
		OCTA K -1 A_SetFloorClip;
		stop;
		
	XDeath:
        TNT1 A 0 A_SpawnItemEx("DNBloodPool", 0, 0, 0, 0, 0, 0, 0, 0, 128); // 128 is the failchance
		TNT1 A 0 A_ThrowGibs;
		stop;
	}
}