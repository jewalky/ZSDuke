class Enforcer : JumpyMonster
{
    void A_EnforcerChase()
    {
        if (!target) return;
        
        if (TryLeap(ResolveState("Leap"), JF_UP))
            return;
    
        if (CheckShrunk()) A_Chase(null, null);
        else A_Chase(null, "Missile");
    }
    
    Default
    {
        Monster;
        Radius 24;
        Height 64;
        Health 100;
        Mass 200;
        PainChance 64;
        Speed 8;
        
        Scale 0.7;
        
        Obituary "%o got perforated by an Enforcer.";
        
        SeeSound "lizman/see";
        PainSound "lizman/pain";
        DeathSound "lizman/death";
        ActiveSound "lizman/roam";
    }

    States
    {
    Spawn:
        ENFO A 8 A_Look;
        loop;
        
    See:
        ENFO AAAABBBBCCCCDDDD 1 A_EnforcerChase;
        loop;
        
    Missile:
        ENFO J 2 { A_FaceTarget(); }
        ENFO K 2 BRIGHT { A_FaceTarget(); A_PlaySound("weapon/chaingun", CHAN_AUTO); A_CustomBulletAttack(4, 4, 1, 2); }
        ENFO K 0 A_MonsterRefire(20, "See");
        loop;
    
    Pain:
        ENFO L 8 A_Pain;
        goto See;
        
    Death:
        TNT1 A 0 A_Scream;
        ENFO LMNO 3;
        ENFO P 3 A_NoBlocking;
        ENFO Q -1 A_SetFloorClip;
        stop;
        
    XDeath:
        TNT1 A 0 A_SpawnItemEx("DNBloodPool", 0, 0, 0, 0, 0, 0, 0, 0, 128); // 128 is the failchance
        TNT1 A 0 A_ThrowGibs;
        stop;
        
    Leap:
        ENFO G 1 A_Leap;
        loop;
    }
}
